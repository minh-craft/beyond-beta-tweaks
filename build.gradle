plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
//		forRepositories(fg.repository) // Only add this if you're using ForgeGradle, otherwise remove this line
		filter {
			includeGroup "maven.modrinth"
		}
	}

	// Mixin Squared
	maven { url = "https://maven.bawnorton.com/releases" }

	// Mirror
	maven { url = "https://maven.enjarai.dev/mirrors" }

	// Mod Menu
	maven {
		name = "Terraformers"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "Wisp Forest"
		url = "https://maven.wispforest.io"
	}

    maven {
        url = "https://cursemaven.com"
    }

	// Better Days Compat
	maven {
		name = 'WhiteNoise' // Library for wendall911 mods
		url = 'https://raw.githubusercontent.com/wendall911/maven/main/'
	}

	// Melancholic Hunger Compat
	maven {
		name = "Ladysnake Mods"
		url = 'https://maven.ladysnake.org/releases'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Mixin Squared
	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.3.7-beta.1")))

	// Owo lib - True Darkness dependence
	modImplementation("io.wispforest:owo-lib:$project.owo_lib_version")
//	annotationProcessor("io.wispforest:owo-lib:$project.owo_lib_version")
	//	include("io.wispforest:owo-sentinel:$project.owo_lib_version")

	// True Darkness
	modImplementation "maven.modrinth:true-darkness-refabricated:$project.true_darkness_refabricated_version"

    // Nostalgic Tweaks Compatibility
	modImplementation "maven.modrinth:nostalgic-tweaks:$project.nostalgic_tweaks_version"

	// Archery Expansion Compatibility
	modImplementation "maven.modrinth:archery-expansion:$project.archery_expansion_version"

	// Better Climbing Compatibility
	modImplementation "maven.modrinth:better-climbing:$project.better_climbing_version"

	// Better Days Compatability
	modImplementation "maven.modrinth:betterdays:$project.better_days_version"
	modImplementation group: 'technology.roughness.whitenoise', name: "whitenoise-${minecraft_version}-fabric", version: "${whitenoise_range}"
	modImplementation group: 'technology.roughness.whitenoise', name: "whitenoise-${minecraft_version}-fabric", version: "${whitenoise_version}"

	// Better Recipe Book Compatibility
	modImplementation "maven.modrinth:brb:$project.brb_version"
	modRuntimeOnly "maven.modrinth:cloth-config:11.1.136+fabric"
	modRuntimeOnly "maven.modrinth:bundle-backportish:1.0.1+mc1.20.1"
    modImplementation "curse.maven:architectury-api-419699:5137936"

	// Melancholic Hunger Compatibility
	modImplementation "maven.modrinth:melancholic_hunger:$project.melancholic_hunger_version"
	// Cardinal Components API
	include (modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.CCA_version}")
	include (modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.CCA_version}")


	// Reach Entity Attributes - for adjusting entity attack range
	include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}")

	// For Config Options
	modImplementation include ("maven.modrinth:midnightlib:${project.midnightlib_version}")
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    modImplementation "curse.maven:supplementaries-412082:7431872"
    modImplementation "curse.maven:selene-499980:7494448"

    modImplementation "curse.maven:scholar-961802:7396149"
    modImplementation "curse.maven:forge-config-api-port-547434:7260491"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

loom {
	accessWidenerPath = file("src/main/resources/beyond-beta-tweaks.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}